dnl ---------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script.

AC_INIT(xawtv.spec.in)
AC_CONFIG_HEADER(config.h)


dnl ---------------------------------------------------------------------
dnl Options

AC_ARG_ENABLE(xfree-ext,
  [  --enable-xfree-ext      use XFree extentions (DGA,VidMode,DPMS)])
AC_ARG_ENABLE(xvideo,
  [  --enable-xvideo         use the Xvideo extention])
AC_ARG_ENABLE(lirc,
  [  --enable-lirc           lirc support])
AC_ARG_ENABLE(quicktime,
  [  --enable-quicktime      quicktime support])
AC_ARG_ENABLE(motif,
  [  --enable-motif          enable experimental motif support])
AC_ARG_ENABLE(aa,
  [  --enable-aa             enable aalib support])
AC_ARG_ENABLE(alsa,
  [  --enable-alsa           enable alsa support])


dnl ---------------------------------------------------------------------
dnl Checks for programs.

AC_PROG_CC
AC_PROG_CPP

AC_PROG_INSTALL
AC_CHECK_PROGS(DEPEND,gccmakedep makedepend,true)

dnl ---------------------------------------------------------------------
dnl do some OS specific stuff here

VBIFLAGS=""
PROGS=""
INST=""
RADIO=""
PLUGINS=""
case "`uname -s`" in
	Linux)
		PLUGINS="drv0-v4l2.so drv1-v4l.so snd-oss.so"
		PROGS="v4l-conf fbtv"
		INST="install-v4l-conf install-fbtv"
		RADIO="radio"
		;;
	OpenBSD | FreeBSD | NetBSD)
		# *BSD has important stuff (from ports) in
		# /usr/local ...
		CFLAGS="$CFLAGS -I/usr/local/include -L/usr/local/lib"
		PLUGINS="drv0-bsd.so snd-oss.so"
		VBIFLAGS="-DBSD"
		;;
	*)
		AC_MSG_CHECKING(if xawtv will build on `uname -s`)
		AC_MSG_RESULT(maybe)
		;;
esac
AC_SUBST(VBIFLAGS)
AC_SUBST(PROGS)
AC_SUBST(INST)
AC_SUBST(RADIO)
AC_SUBST(PLUGINS)


dnl ---------------------------------------------------------------------
dnl Checks for functions

AC_EGREP_HEADER(sockaddr_storage,sys/socket.h,AC_DEFINE(HAVE_SOCKADDR_STORAGE))
AC_CHECK_HEADERS(endian.h getopt.h soundcard.h sys/soundcard.h alsa/asoundlib.h linux/joystick.h dev/ic/bt8xx.h machine/ioctl_bt848.h)
AC_CHECK_FUNCS(ftello fseeko getpt getnameinfo getopt_long dlopen)

AC_SUBST(DLFLAGS)
DLFLAGS=""
if test "$ac_cv_func_dlopen" = "no"; then
  AC_CHECK_LIB(dl,dlopen, [ DLFLAGS="-ldl" ] )
fi
AC_MSG_CHECKING(for ELF)
if test -z "`echo __ELF__ | $CC -E - | grep __ELF__`"; then
  AC_MSG_RESULT(yes)
  DLFLAGS="$DLFLAGS -Wl,-E"
else
  AC_MSG_RESULT(no)
fi

AC_CHECK_LIB(pthread,pthread_create,LIBPTHREAD="-lpthread")
if test "$LIBPTHREAD" = ""; then
  AC_CHECK_LIB(c_r,pthread_create,LIBPTHREAD="-lc_r")
fi
AC_CHECK_LIB(ossaudio,main,LIBOSS="-lossaudio")
AC_CHECK_LIB(ncurses,initscr,LIBCURSES="-lncurses")
if test "$LIBCURSES" = ""; then
  AC_CHECK_LIB(curses,initscr,LIBCURSES="-lcurses")
fi
if test "$LIBCURSES" = ""; then
  echo "Oops: (n)curses library not found.  You need this one, please install."
  echo "Note: to compile stuff just the library packages are not enougth,"
  echo "      you need also the *-devel packages."
  exit 1
fi
AC_SUBST(LIBPTHREAD)
AC_SUBST(LIBOSS)
AC_SUBST(LIBCURSES)


dnl ---------------------------------------------------------------------
dnl X11 checks

AC_PATH_XTRA
if test "$no_x" != "yes"; then
  PROGS="$PROGS xawtv xawtv-remote xvideo rootv"
  ATHENA="-lXaw -lXmu -lXt $X_PRE_LIBS -lXpm -lXext -lX11 $X_EXTRA_LIBS"
  FSLIB="-lFS"
  SUBS="font"
  TOOLS="propwatch"
else
  ATHENA=""
  FSLIB=""
  SUBS=""
  TOOLS=""
fi
AC_SUBST(X_CFLAGS)
AC_SUBST(X_PRE_LIBS)
AC_SUBST(X_EXTRA_LIBS)
AC_SUBST(X_LIBS)
AC_SUBST(x_includes)
AC_SUBST(x_libraries)
AC_SUBST(ATHENA)
AC_SUBST(FSLIB)
AC_SUBST(SUBS)
AC_SUBST(TOOLS)

AC_CHECK_LIB(jpeg, jpeg_start_compress, JPEG="found",,)
if test "$JPEG" != "found"; then
    echo "Oops:	jpeg library not found.  You need this one, please install."
    echo "Note:	to compile stuff just the library packages are not enougth,"
    echo "	you need also the *-devel packages."
    exit 1
fi

AC_SUBST(LIBALSA)
LIBALSA=""
if test "$enable_alsa" != "no"; then
    AC_CHECK_LIB(asound, snd_seq_open,
	AC_DEFINE(HAVE_ALSA) PROGS="$PROGS mididump"; LIBALSA="-lasound",,)
else
    echo "*** alsa disabled"
fi

AC_SUBST(LIRC)
LIRC=""
if test "$enable_lirc" != "no"; then
    AC_CHECK_LIB(lirc_client, lirc_init,
	AC_DEFINE(HAVE_LIBLIRC_CLIENT) LIRC="-llirc_client",,)
else
    echo "*** lirc disabled"
fi

AC_SUBST(AALIBS)
AALIBS=""
if test "$enable_aa" != "no"; then
    AC_CHECK_LIB(aa,aa_autoinit,
	[ PROGS="$PROGS ttv"; INST="$INST install-ttv"; AA="found" ],,)
    if test "$AA" = "found"; then
        AALIBS=-laa
        if test -x "`which aalib-config 2>/dev/null`"; then
            AALIBS=`aalib-config --libs`
        fi
    fi
else
    echo "*** aalib support disabled"
fi

if test "$enable_quicktime" != "no"; then
    AC_CHECK_LIB(png, png_read_info, PNG="found")
    if test "$PNG" = "found"; then
	AC_CHECK_LIB(quicktime, quicktime_open,
		QUICKTIME="found";AC_DEFINE(HAVE_LIBQUICKTIME),,
		-ldl -lglib -ljpeg -lpng $LIBPTHREAD)
    else
	echo "*** libpng not found: no quicktime support, sorry"
    fi
else
    echo "*** quicktime disabled"
fi
if test "$QUICKTIME" = "found"; then
    QTLIBS="-lquicktime -ldl -lglib -lpng -lz -lm" 
    PLUGINS="$PLUGINS write-qt.so"
else
    QTLIBS=""
fi
AC_SUBST(QTLIBS)

if test "$enable_xfree_ext" != "no"; then
    AC_CHECK_LIB(Xxf86dga, XF86DGAQueryExtension,,, 
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
    AC_CHECK_LIB(Xxf86vm, XF86VidModeQueryExtension,,, 
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
    AC_CHECK_LIB(Xdpms, DPMSQueryExtension,,, 
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
    if test "$ac_cv_lib_Xdpms_DPMSQueryExtension" = "no"; then
	AC_CHECK_LIB(Xext, DPMSQueryExtension,AC_DEFINE(HAVE_LIBXDPMS),,
	    $X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
    fi
    AC_CHECK_LIB(Xinerama, XineramaQueryExtension,,,
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
else
    echo "*** XFree extentions disabled"
fi

if test "$enable_xvideo" != "no"; then
    AC_CHECK_LIB(Xv, XvQueryExtension,,,
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
else
    echo "*** Xvideo extention disabled"
fi

if test "$enable_motif" != "no"; then
    AC_CHECK_LIB(Xm,XmStringGenerate,
	[ PROGS="$PROGS motv"; INST="$INST install-motv" ],,
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
else
    echo "*** motif support disabled"
fi

VERSION="`(cd $srcdir; pwd) | sed -e 's/.*-//'`"
AC_SUBST(VERSION)

dnl ---------------------------------------------------------------------
AC_MSG_CHECKING(for X11 config directory)
x11conf=/usr/X11R6/lib/X11
if test -d /etc/X11; then
	x11conf=/etc/X11
fi
AC_MSG_RESULT($x11conf)
AC_SUBST(x11conf)

AC_MSG_CHECKING(for X11 app-defaults directory)
resdir=/usr/X11R6/lib/X11
if test -d /etc/X11/app-defaults; then
	resdir=/etc/X11
fi
AC_MSG_RESULT($resdir/app-defaults)
AC_SUBST(resdir)


dnl ---------------------------------------------------------------------
AC_OUTPUT(Make.config Makefile src/Makefile tools/Makefile radio/Makefile 
	man/Makefile i2c/Makefile font/Makefile webcam/Makefile
	http/Makefile libvbi/Makefile libng/Makefile libng/plugins/Makefile
	cc/Makefile xawtv.spec)

