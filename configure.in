dnl ---------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script.

AC_INIT(xawtv.spec.in)
AC_CONFIG_HEADER(config.h)


dnl ---------------------------------------------------------------------
dnl Options

AC_ARG_ENABLE(jpeg,
  [  --enable-jpeg           jpeg support (for snapshots)])
AC_ARG_ENABLE(xfree-ext,
  [  --enable-xfree-ext      use XFree extentions (DGA,VidMode,DPMS)])
AC_ARG_ENABLE(xvideo,
  [  --enable-xvideo         use the Xvideo extention])
AC_ARG_ENABLE(lirc,
  [  --enable-lirc           lirc support])


dnl ---------------------------------------------------------------------
dnl Checks for programs.

AC_PROG_CC
AC_PROG_CPP

AC_PROG_INSTALL
AC_CHECK_PROGS(DEPEND,gccmakedep makedepend,true)


dnl ---------------------------------------------------------------------
dnl X11 checks

AC_PATH_XTRA
if test "-DX_DISPLAY_MISSING" = "$X_CFLAGS"; then
  AC_MSG_ERROR(can not find X11)
fi
AC_SUBST(X_CFLAGS)
AC_SUBST(X_PRE_LIBS)
AC_SUBST(X_EXTRA_LIBS)
AC_SUBST(X_LIBS)
AC_SUBST(x_libraries)

AC_CHECK_LIB(Xaw3d, Xaw3dComputeTopShadowRGB, 
  XAWLIB=Xaw3d, XAWLIB=Xaw, 
  $X_LIBS $X_PRE_LIBS -lXext -lXmu -lXt -lX11)
AC_SUBST(XAWLIB)

AC_SUBST(JPEG)
JPEG=""
if test "$enable_jpeg" != "no"; then
    AC_CHECK_LIB(jpeg, jpeg_start_compress,AC_DEFINE(HAVE_LIBJPEG) JPEG="-ljpeg",,)
else
    echo "*** jpeg disabled"
fi

AC_SUBST(LIRC)
LIRC=""
if test "$enable_lirc" != "no"; then
    AC_CHECK_LIB(lirc_client, lirc_init,
	AC_DEFINE(HAVE_LIBLIRC_CLIENT) LIRC="-llirc_client",,)
else
    echo "*** lirc disabled"
fi

if test "$enable_xfree_ext" != "no"; then
    AC_CHECK_LIB(Xxf86dga, XF86DGAQueryExtension,,, 
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
    AC_CHECK_LIB(Xxf86vm, XF86VidModeQueryExtension,,, 
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
    AC_CHECK_LIB(Xdpms, DPMSQueryExtension,,, 
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
else
    echo "*** XFree extentions disabled"
fi

if test "$enable_xvideo" != "no"; then
    AC_CHECK_LIB(Xv, XvQueryExtension,,,
	$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS)
else
    echo "*** Xvideo extention disabled"
fi

AC_CHECK_LIB(Xext, XShmAttach,AC_DEFINE(HAVE_MITSHM),,
	$X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS)


XAWTV_VERSION="`pwd | sed -e 's/.*xawtv-\([[a-z0-9.]]\+\).*/\1/'`"
AC_SUBST(XAWTV_VERSION)

dnl ---------------------------------------------------------------------
AC_OUTPUT(Makefile src/Makefile tools/Makefile radio/Makefile 
	man/Makefile i2c/Makefile font/Makefile webcam/Makefile xawtv.spec)
